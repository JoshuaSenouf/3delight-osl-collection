/*
*/
shader dlLog(
	color inputColor = color(0, 0, 0)
        [[  string help = "Input color that will be used to produce its logarithm, and return it as the output color."
                "<br/><br/>Formula: <b>log(base) (input)</b>",
            string label = "Input",
            string widget = "number" ]],
	float inputColorR = 0
        [[  string label = "Input R",
            string widget = "null" ]],
	float inputColorG = 0
        [[  string label = "Input G",
            string widget = "null" ]],
	float inputColorB = 0
        [[  string label = "Input B",
            string widget = "null" ]],
	color inputBase = color(2.7183, 2.7183, 2.7183)
        [[  string help = "Input logarithmic base, defaulting to the natural logarithmic base, e."
                "<br/><br/>Formula: <b>log(base) (input)</b>",
            string label = "Base",
            string widget = "number" ]],
	float inputBaseR = 0
        [[  string label = "Base R",
            string widget = "null" ]],
	float inputBaseG = 0
        [[  string label = "Base G",
            string widget = "null" ]],
	float inputBaseB = 0
        [[  string label = "Base B",
            string widget = "null" ]],
    int useRedChannel = 1
        [[  string help = "Define whether or not the red channel should be affected."
                "<br/><br/>If turned off, the channel will instead output the untouched value coming from the "
                "chosen input.",
            string label = "Red",
            string page = "Channels",
            string widget = "checkBox" ]],
    int useGreenChannel = 1
        [[  string help = "Define whether or not the green channel should be affected."
                "<br/><br/>If turned off, the channel will instead output the untouched value coming from the "
                "chosen input.",
            string label = "Green",
            string page = "Channels",
            string widget = "checkBox" ]],
    int useBlueChannel = 1
        [[  string help = "Define whether or not the blue channel should be affected."
                "<br/><br/>If turned off, the channel will instead output the untouched value coming from the "
                "chosen input.",
            string label = "Blue",
            string page = "Channels",
            string widget = "checkBox" ]],

	output color outColor = color(0, 0, 0),
	output float outColorR = 0,
	output float outColorG = 0,
	output float outColorB = 0)
{
    color localInputColor = inputColor;
    color localInputBase = inputBase;

    // We check if anything is connected to the per-channel shader parameters,
    // and if so, we prioritize them over the values coming from the color parameters,
    // and assign them to their respective channels.
    if (isconnected(inputColorR))
        localInputColor[0] = inputColorR;
    if (isconnected(inputColorG))
        localInputColor[1] = inputColorG;
    if (isconnected(inputColorB))
        localInputColor[2] = inputColorB;
    if (isconnected(inputBaseR))
        localInputBase[0] = inputBaseR;
    if (isconnected(inputBaseG))
        localInputBase[1] = inputBaseG;
    if (isconnected(inputBaseB))
        localInputBase[2] = inputBaseB;

    // We are computing each channel individually depending on which the user wants to affect.
    // Instead of computing the whole vector using the same logarithmic base, we let
    // the user the ability to use a different base per color channel.
    outColor = color(useRedChannel ? log(localInputColor[0], localInputBase[0]) : localInputColor[0],
        useGreenChannel ? log(localInputColor[1], localInputBase[1]) : localInputColor[1],
        useBlueChannel ? log(localInputColor[2], localInputBase[2]) : localInputColor[2]);

    outColorR = outColor[0];
    outColorG = outColor[1];
    outColorB = outColor[2];
}
