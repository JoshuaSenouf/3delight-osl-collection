/*
    Check for each of the per-channel parameters of an input color if they are connected to an upstream node
    in the shading network, and if so, we use that value instead of the one coming from the color parameter instead.
*/
color getAggregateColor(color inputColor, float inputColorR, float inputColorG, float inputColorB)
{
    color aggregateColor = inputColor;

    if (isconnected(inputColorR))
        aggregateColor[0] = inputColorR;
    if (isconnected(inputColorG))
        aggregateColor[1] = inputColorG;
    if (isconnected(inputColorB))
        aggregateColor[2] = inputColorB;

    return aggregateColor;
}


/*
*/
shader dlExp(
	color inputExponent = color(0)
        [[  string help = "Input exponent that will be used to produce the exponential of that exponent, and return "
                "it as the output color."
                "<br/><br/>Formula: <b>exp(exponent)</b>",
            string label = "Input",
            string widget = "number" ]],
	float inputExponentR = 0
        [[  string label = "Input R",
            string widget = "null" ]],
	float inputExponentG = 0
        [[  string label = "Input G",
            string widget = "null" ]],
	float inputExponentB = 0
        [[  string label = "Input B",
            string widget = "null" ]],
    int useRedChannel = 1
        [[  string help = "Define whether or not the red channel should be affected."
                "<br/><br/>If turned off, the channel will instead output the value 0.",
            string label = "Red",
            string page = "Channels",
            string widget = "checkBox" ]],
    int useGreenChannel = 1
        [[  string help = "Define whether or not the green channel should be affected."
                "<br/><br/>If turned off, the channel will instead output the value 0.",
            string label = "Green",
            string page = "Channels",
            string widget = "checkBox" ]],
    int useBlueChannel = 1
        [[  string help = "Define whether or not the blue channel should be affected."
                "<br/><br/>If turned off, the channel will instead output the value 0.",
            string label = "Blue",
            string page = "Channels",
            string widget = "checkBox" ]],

	output color outColor = color(0),
	output float outColorR = 0,
	output float outColorG = 0,
	output float outColorB = 0)
{
    // We check with the "getAggregateColor()" function if anything is connected to the per-channel
    // shader parameters, and if so, we prioritize them over the values coming from the color parameter,
    // and assign them to their respective channels.
    color localInputExponent = getAggregateColor(inputExponent, inputExponentR, inputExponentG, inputExponentB);

    // We are computing each channel individually depending on which the user wants to affect.
    outColor = color(
        useRedChannel ? exp(localInputExponent[0]) : 0,
        useGreenChannel ? exp(localInputExponent[1]) : 0,
        useBlueChannel ? exp(localInputExponent[2]) : 0
    );

    outColorR = outColor[0];
    outColorG = outColor[1];
    outColorB = outColor[2];
}
