#include "3delightosl.h"

#include "../../include/jsUtils.h"


/*
*/
surface jsLobeCoat
(
    color i_color_rgb = color(0.8)
        [[  string help = "Color of the coat that will be used to shade the object.",
            string label = "Color",
            string widget = "number" ]],
    float i_color_r = 0.0
        [[  string label = "Color R",
            string widget = "null" ]],
    float i_color_g = 0.0
        [[  string label = "Color G",
            string widget = "null" ]],
    float i_color_b = 0.0
        [[  string label = "Color B",
            string widget = "null" ]],
    float i_roughness = 0.3
        [[  string help = "Roughness of the coat.",
            string label = "Roughness",
            string page = "Microfacet",
            string widget = "number",
            float min = 0.0,
            float max = 1.0 ]],
    float i_ior = 1.5
        [[  string help = "IOR of the coat.",
            string label = "IOR",
            string page = "Microfacet",
            string widget = "number",
            float min = 0.0,
            float max = 5.0 ]],
    normal i_shading_normal = N
        [[  string label = "Normal",
            string widget = "null" ]],

    output closure color outColor = 0
)
{
    color color_rgb = getComposedColor(i_color_rgb, i_color_r, i_color_g, i_color_b);
    float roughness = i_roughness * i_roughness;
    float ior = max(0.0001, i_ior);
    normal shading_normal = normalize(i_shading_normal);
    float fresnel_reflectance = getFresnel(shading_normal, ior, 0);

    closure color specular_aov = outputvariable(
        "reflection",
        microfacet("ggx", shading_normal, roughness, ior, 0) * color_rgb * fresnel_reflectance
    );

    // TODO: Support for the anisotropic variant of the microfacet closure.
    outColor = specular_aov;
}
