/*
*/
surface jsLobeLayering
(
    int i_mode = 0
        [[  string help = "Define if the layering should be done using a physically plausible or artistic approach."
                "<br/><br/>The 'Physical' mode uses a single weight parameter that will couple both lobes together, "
                "ensuring that they won't output more energy then they should."
                "<br/><br/>The 'Artistic' mode uses two non-coupled, purely additive weight parameters. This of course "
                "implies that it is *very* easy to break energy conservation using this mode, and as such it "
                "should be used with caution.",
            string label = "Mode",
            string widget = "mapper",
            string options = "Physical:0|Artistic:1" ]],
    float i_physical_weight = 0.5
        [[  string help = "Define how much the top lobe will layer itself over the bottom lobe.",
            string label = "Weight",
            string page = "Physical",
            string widget = "number",
            float min = 0.0,
            float max = 1.0 ]],
    float i_artistic_top_weight = 0.5
        [[  string help = "Define the weight of the top lobe.",
            string label = "Top Weight",
            string page = "Artistic",
            string widget = "number",
            float min = 0.0,
            float max = 1.0 ]],
    float i_artistic_bottom_weight = 0.5
        [[  string help = "Define the weight of the bottom lobe.",
            string label = "Bottom Weight",
            string page = "Artistic",
            string widget = "number",
            float min = 0.0,
            float max = 1.0 ]],
    closure color i_top_lobe = 0
        [[  string label = "Top Lobe",
            string widget = "null" ]],
    closure color i_bottom_lobe = 0
        [[  string label = "Bottom Lobe",
            string widget = "null" ]],

    output closure color outColor = 0
)
{
    // If we are using the "Physical" mode...
    if (i_mode == 0)
    {
        float physical_weight = clamp(i_physical_weight, 0.0, 1.0);

        outColor = (i_top_lobe * physical_weight) + (i_bottom_lobe * (1.0 - physical_weight));
    }
    // If we are using the "Artistic" mode...
    else
    {
        float artistic_top_weight = clamp(i_artistic_top_weight, 0.0, 1.0);
        float artistic_bottom_weight = clamp(i_artistic_bottom_weight, 0.0, 1.0);

        outColor = (i_top_lobe * artistic_top_weight) + (i_bottom_lobe * artistic_bottom_weight);
    }
}
