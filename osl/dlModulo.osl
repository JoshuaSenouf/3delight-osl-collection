/*
*/
shader dlModulo(
	color inputColor = color(0)
        [[  string help = "Input color that will be used to produce a modulo, and return the result as the output "
                "color."
                "<br/><br/>Formula: <b>mod(input, divisor)</b>",
            string label = "Input",
            string widget = "number" ]],
	float inputColorR = 0
        [[  string label = "Input R",
            string widget = "null" ]],
	float inputColorG = 0
        [[  string label = "Input G",
            string widget = "null" ]],
	float inputColorB = 0
        [[  string label = "Input B",
            string widget = "null" ]],
	color inputDivisor = color(0)
        [[  string help = "Input divisor by which we are going to modulo the input color."
                "<br/><br/>Formula: <b>mod(input, divisor)</b>",
            string label = "Divisor",
            string widget = "number" ]],
	float inputDivisorR = 0
        [[  string label = "Divisor R",
            string widget = "null" ]],
	float inputDivisorG = 0
        [[  string label = "Divisor G",
            string widget = "null" ]],
	float inputDivisorB = 0
        [[  string label = "Divisor B",
            string widget = "null" ]],
    int useRedChannel = 1
        [[  string help = "Define whether or not the red channel should be affected."
                "<br/><br/>If turned off, the channel will instead output the untouched value coming from the "
                "chosen input.",
            string label = "Red",
            string page = "Channels",
            string widget = "checkBox" ]],
    int useGreenChannel = 1
        [[  string help = "Define whether or not the green channel should be affected. If turned off, the channel "
                "will instead output the untouched value coming from the chosen input.",
            string label = "Green",
            string page = "Channels",
            string widget = "checkBox" ]],
    int useBlueChannel = 1
        [[  string help = "Define whether or not the blue channel should be affected. If turned off, the channel "
                "will instead output the untouched value coming from the chosen input.",
            string label = "Blue",
            string page = "Channels",
            string widget = "checkBox" ]],

	output color outColor = color(0),
	output float outColorR = 0,
	output float outColorG = 0,
	output float outColorB = 0)
{
    color localInputColor = inputColor;
    color localInputDivisor = inputDivisor;

    // We check if anything is connected to the per-channel shader parameters,
    // and if so, we prioritize them over the values coming from the color parameters,
    // and assign them to their respective channels.
    if (isconnected(inputColorR))
        localInputColor[0] = inputColorR;
    if (isconnected(inputColorG))
        localInputColor[1] = inputColorG;
    if (isconnected(inputColorB))
        localInputColor[2] = inputColorB;
    if (isconnected(inputDivisorR))
        localInputDivisor[0] = inputDivisorR;
    if (isconnected(inputDivisorG))
        localInputDivisor[1] = inputDivisorG;
    if (isconnected(inputDivisorB))
        localInputDivisor[2] = inputDivisorB;

    // We are computing each channel individually depending on which the user wants to affect.
    outColor = color(
        useRedChannel ? mod(localInputColor[0], localInputDivisor[0]) : localInputColor[0],
        useGreenChannel ? mod(localInputColor[1], localInputDivisor[1]) : localInputColor[1],
        useBlueChannel ? mod(localInputColor[2], localInputDivisor[2]) : localInputColor[2]);

    outColorR = outColor[0];
    outColorG = outColor[1];
    outColorB = outColor[2];
}
